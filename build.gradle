/*
 * Copyright (c) 2016. Fengguo Wei and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Detailed contributors are listed in the CONTRIBUTOR.md
 */

import org.argus.jawa.gradle.JawaPluginIntegrationTestTask

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.5"
    }
}

apply plugin: "java"
apply plugin: "groovy"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "idea"
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"

apply from: "dependencies.gradle"

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile libraries.commons_io
    compile libraries.groovy
    compile libraries.jawa_compiler
    testCompile libraries.spock
    testCompile libraries.jmock
}


group = "com.github.arguslab"
version = "1.0.5-SNAPSHOT"
description = """Jawa language compiler for Gradle.  Built with Gradle $project.gradle.gradleVersion.
Projects applying this plugin require a compile-time dependency on the jawa-compiler library.  Please include this in the depencencies closure:
dependencies {
    compile group: 'com.github.arguslab', name: 'jawa-compiler_2.11', version: '1.0.3'
}
"""

final boolean isSnapshot = project.version.endsWith('-SNAPSHOT')

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

pluginBundle {
    website = 'https://github.com/arguslab/gradle-jawa-plugin'
    vcsUrl = 'https://github.com/arguslab/gradle-jawa-plugin.git'
    description = project.description
    tags = ['jawa']

    plugins {
        jawaPlugin {
            id = 'org.argus.jawa'
            displayName = 'Jawa Gradle Plugin'
        }
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    description 'generate sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    description 'generate javadoc'
    from javadoc
}

artifacts {
    archives sourceJar
    archives javadocJar
}

task integTest(type: JawaPluginIntegrationTestTask, dependsOn: "uploadArchives")

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

def uploadToSonatypeRepository = hasProperty("sonatypeUserName")

/* from 'maven' plugin */
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> if(!isSnapshot) { signing.signPom(deployment) } }
            snapshotRepository(url: uri("gh-pages/repository/snapshot"))
            if (uploadToSonatypeRepository) {
                def repositoryUrl = isSnapshot ? "https://oss.sonatype.org/content/repositories/snapshots"
                        : "https://oss.sonatype.org/service/local/staging/deploy/maven2"

                repository(url: repositoryUrl) {
                    authentication(userName: sonatypeUserName, password: sonatypePassword)
                }

                pom.project {
                    name project.name
                    packaging 'jar'
                    description project.description
                    url "https://github.com/arguslab/gradle-jawa-plugin"

                    scm {
                        url "scm:git@github.com:arguslab/gradle-jawa-plugin.git"
                        connection "scm:git@github.com:arguslab/gradle-jawa-plugin.git"
                        developerConnection "scm:git@github.com:arguslab/gradle-jawa-plugin.git"
                    }

                    licenses {
                        license {
                            name "Eclipse-1.0"
                            url "http://www.opensource.org/licenses/eclipse-1.0.php"
                        }
                    }

                    developers {
                        developer {
                            id "fgwei"
                            name "Fengguo (Hugo) Wei"
                            email 'fgwei521@gmail.com'
                            organization 'arguslab'
                        }
                    }
                }
            }
        }
    }
}

signing {
    required { !isSnapshot && gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

// the gradle plugin portal isn't compatible with SNAPSHOT versions
// https://discuss.gradle.org/t/uploading-snapshot-versions-to-the-plugin-portal/11347
if (isSnapshot) {
    signArchives.enabled = false
    publishPlugins.enabled = false
} else {
    signArchives.enabled = true
    publishPlugins.enabled = true
}